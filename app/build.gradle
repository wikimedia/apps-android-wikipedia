import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'com.android.application'
    id 'com.google.devtools.ksp'
    id 'com.google.gms.google-services'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlinx-serialization'
    alias(libs.plugins.compose.compiler)
    alias(libs.plugins.allure.adapter)
    alias(libs.plugins.allure.report)
    alias(libs.plugins.allure.download)
}

// Copy the signing.properties.sample file to ~/.sign/signing.properties and adjust the values.
final File PROD_PROPS_FILE = new File(System.getProperty('user.home'), '.sign/signing.properties')
final File REPO_PROPS_FILE = new File('repo.properties')
final Properties PROD_PROPS = loadProperties(PROD_PROPS_FILE)
final Properties REPO_PROPS = loadProperties(REPO_PROPS_FILE)

static def computeVersionName(versionCode, label) {
    return "${versionCode}-${label}-${(new Date()).format('yyyy-MM-dd')}"
}

android {
    compileSdk = 36

    compileOptions {
        coreLibraryDesugaringEnabled = true

        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlin {
        compilerOptions {
            jvmTarget = JvmTarget.JVM_17
        }
    }

    defaultConfig {
        applicationId 'org.wikipedia'
        minSdk = 23
        targetSdk = 36
        versionCode 50564
        testApplicationId 'org.wikipedia.test'
        testInstrumentationRunner "io.qameta.allure.android.runners.AllureAndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        vectorDrawables.useSupportLibrary = true

        signingConfig signingConfigs.debug

        buildConfigField "String", "DEFAULT_RESTBASE_URI_FORMAT", '"%1$s://%2$s/api/rest_v1/"'
        buildConfigField "String", "META_WIKI_BASE_URI", '"https://meta.wikimedia.org"'
        buildConfigField "String", "EVENTGATE_ANALYTICS_EXTERNAL_BASE_URI", '"https://intake-analytics.wikimedia.org"'
        buildConfigField "String", "EVENTGATE_LOGGING_EXTERNAL_BASE_URI", '"https://intake-logging.wikimedia.org"'
        def TEST_LOGIN_USERNAME = System.getenv('TEST_LOGIN_USERNAME')
        def TEST_LOGIN_PASSWORD = System.getenv('TEST_LOGIN_PASSWORD')
        buildConfigField "String", "TEST_LOGIN_USERNAME", TEST_LOGIN_USERNAME != null ? "\"${TEST_LOGIN_USERNAME}\"" : '"Foo"'
        buildConfigField "String", "TEST_LOGIN_PASSWORD", TEST_LOGIN_PASSWORD != null ? "\"${TEST_LOGIN_PASSWORD}\"" : '"Bar"'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
        animationsDisabled = true
    }

    buildFeatures {
        viewBinding true
        buildConfig true
        compose true
    }

    androidResources {
        generateLocaleConfig = true
    }

    sourceSets {

        [ prod, beta, alpha, dev, custom ].forEach {
            it.java.srcDirs += 'src/extra/java'
            it.res.srcDirs += 'src/extra/res'
        }

        androidTest {
            assets.srcDirs += files("$projectDir/schemas".toString())
        }
    }

    signingConfigs {
        prod {
            setSigningConfigKey(prod, PROD_PROPS)
        }
        debug {
            setSigningConfigKey(debug, REPO_PROPS)
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            testProguardFiles 'test-proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            testProguardFiles 'test-proguard-rules.pro'
        }
    }

    flavorDimensions = ['default']

    productFlavors {
        dev {
            versionName computeVersionName(defaultConfig.versionCode, 'dev')
            applicationIdSuffix '.dev'
            buildConfigField "String", "META_WIKI_BASE_URI", '"https://meta.wikimedia.beta.wmflabs.org"'
            buildConfigField "String", "EVENTGATE_ANALYTICS_EXTERNAL_BASE_URI", '"https://intake-analytics.wikimedia.beta.wmflabs.org"'
            buildConfigField "String", "EVENTGATE_LOGGING_EXTERNAL_BASE_URI", '"https://intake-logging.wikimedia.beta.wmflabs.org"'
        }
        prod {
            versionName computeVersionName(defaultConfig.versionCode, 'r')
            signingConfig signingConfigs.prod
        }
        alpha {
            versionName computeVersionName(defaultConfig.versionCode, 'alpha')
            applicationIdSuffix '.alpha'
        }
        beta {
            versionName computeVersionName(defaultConfig.versionCode, 'beta')
            applicationIdSuffix '.beta'
            signingConfig signingConfigs.prod
        }
        fdroid {
            versionName computeVersionName(defaultConfig.versionCode, 'fdroid')
            signingConfig signingConfigs.prod
        }
        custom {
            versionName computeVersionName(defaultConfig.versionCode, customChannel)
            // next line is for injecting a custom channel value into the custom/AndroidManifest.xml
            manifestPlaceholders = [customChannel:getProperty('customChannel').toString()]
            signingConfig signingConfigs.prod
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    bundle {
        language {
            enableSplit false
        }
    }
    namespace 'org.wikipedia'
}

ksp {
    arg("room.schemaLocation", "$projectDir/schemas")
}

configurations {
    compileClasspath.extendsFrom(implementation)
}

apply from: '../gradle/src/test.gradle'
apply from: '../gradle/src/ktlint.gradle'

dependencies {

    // To keep the Maven Central dependencies up-to-date
    // use http://gradleplease.appspot.com/ or http://search.maven.org/.
    // Debug with ./gradlew -q app:dependencies --configuration compile

    coreLibraryDesugaring libs.desugar.jdk.libs

    implementation libs.kotlin.stdlib.jdk8
    implementation libs.kotlinx.coroutines.core
    implementation libs.kotlinx.coroutines.android
    implementation libs.kotlinx.serialization.json

    implementation libs.material
    implementation libs.appcompat
    implementation libs.core.ktx
    implementation libs.browser
    implementation libs.constraintlayout
    implementation libs.fragment.ktx
    implementation libs.paging.runtime.ktx
    implementation libs.palette.ktx
    implementation libs.preference.ktx
    implementation libs.recyclerview
    implementation libs.viewpager2
    implementation libs.flexbox
    implementation libs.drawerlayout
    implementation libs.swiperefreshlayout
    implementation libs.work.runtime.ktx

    implementation libs.okhttp.tls
    implementation libs.okhttp3.logging.interceptor
    implementation libs.retrofit
    implementation libs.commons.lang3
    implementation libs.jsoup
    implementation libs.photoview
    implementation libs.balloon
    implementation libs.retrofit2.kotlinx.serialization.converter
    implementation(libs.paging.compose)

    implementation libs.maplibre
    implementation libs.maplibre.annotation

    implementation libs.androidx.room.runtime
    annotationProcessor libs.androidx.room.compiler
    ksp libs.androidx.room.compiler
    implementation libs.androidx.room.ktx

    // For language detection during editing
    prodImplementation libs.com.google.mlkit.language.id
    betaImplementation libs.com.google.mlkit.language.id
    alphaImplementation libs.com.google.mlkit.language.id
    devImplementation libs.com.google.mlkit.language.id
    customImplementation libs.com.google.mlkit.language.id

    // For receiving push notifications for logged-in users.
    prodImplementation libs.com.google.firebase.firebase.messaging.ktx3
    betaImplementation libs.com.google.firebase.firebase.messaging.ktx3
    alphaImplementation libs.com.google.firebase.firebase.messaging.ktx3
    devImplementation libs.com.google.firebase.firebase.messaging.ktx3
    customImplementation libs.com.google.firebase.firebase.messaging.ktx3

    // For integrating with Google Pay for donations
    prodImplementation libs.com.google.android.gms.play.services.wallet2
    betaImplementation libs.com.google.android.gms.play.services.wallet2
    alphaImplementation libs.com.google.android.gms.play.services.wallet2
    devImplementation libs.com.google.android.gms.play.services.wallet2
    customImplementation libs.com.google.android.gms.play.services.wallet2

    // For InstallReferrer Library
    prodImplementation libs.installreferrer
    betaImplementation libs.installreferrer
    alphaImplementation libs.installreferrer
    devImplementation libs.installreferrer
    customImplementation libs.installreferrer

    debugImplementation libs.leakcanary.android
    implementation libs.plumber.android

    testImplementation libs.junit
    testImplementation libs.mockito.inline
    testImplementation libs.robolectric
    testImplementation libs.okhttp3.okhttp
    testImplementation libs.mockwebserver
    testImplementation libs.hamcrest
    testImplementation libs.room.testing
    testImplementation libs.allure.kotlin.model
    testImplementation libs.allure.kotlin.commons
    testImplementation libs.allure.kotlin.junit4
    androidTestImplementation libs.qameta.allure.kotlin.android
    androidTestImplementation libs.espresso.core
    androidTestImplementation libs.espresso.contrib
    androidTestImplementation libs.androidx.espresso.intents
    androidTestImplementation libs.espresso.web
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.uiautomator
    androidTestImplementation libs.room.testing
    androidTestUtil libs.androidx.orchestrator

    // Coil
    implementation(libs.coil.compose)
    implementation(libs.coil.network.okhttp)
    implementation(libs.coil.gif)
    implementation(libs.coil.svg)

    // Compose Library
    def composeBom = platform(libs.composeBom)
    implementation(composeBom)
    implementation(libs.compose.material3)
    implementation(libs.compose.ui)
    implementation(libs.compose.ui.tooling.preview)
    debugImplementation(libs.compose.ui.tooling)
    implementation(libs.compose.activity)
    implementation(libs.compose.view.model)
    implementation(libs.androidx.navigation.compose)

    // Compose Test Library
    androidTestImplementation(libs.compose.test)
    androidTestImplementation(composeBom)
    debugImplementation(libs.compose.debug.test)
    debugImplementation(composeBom)
}

// Allure configuration
allure {
    version = "2.34.1"
    adapter {
        autoconfigure.set(true)
        aspectjWeaver.set(true)
        // Results will be copied to build/allure-results by copyAllureResults task
    }
    report {
        reportDir.set(file("$buildDir/reports/allure-report"))
        dependsOnTests.set(false)
    }
    commandline {
        // Ensure commandline is downloaded automatically
    }
}

// Task to copy pulled results to build directory before report generation
tasks.register('copyAllureResults') {
    group = 'reporting'
    description = 'Copies Allure results from project root to build directory'
    
    doLast {
        def pulledResults = file("${project.rootDir}/allure-results")
        // Allure Gradle plugin looks for results in build/allure-results by default
        def buildResults = file("${buildDir}/allure-results")
        
        // Handle nested allure-results directory (common when pulling from device)
        def nestedResults = file("${project.rootDir}/allure-results/allure-results")
        def sourceDir = pulledResults
        
        if (nestedResults.exists() && nestedResults.listFiles()) {
            sourceDir = nestedResults
            println "üìÅ Found nested allure-results directory"
        }
        
        if (sourceDir.exists() && sourceDir.listFiles()) {
            // Clean and recreate the directory to ensure fresh copy
            if (buildResults.exists()) {
                delete buildResults
            }
            buildResults.mkdirs()
            
            copy {
                from sourceDir
                into buildResults
            }
            println "‚úÖ Copied ${sourceDir.listFiles().length} result files to ${buildResults.absolutePath}"
            println "   Files: ${buildResults.listFiles().collect { it.name }.join(', ')}"
        } else {
            println "‚ö†Ô∏è  No pulled results found at ${pulledResults.absolutePath}"
        }
    }
    
    outputs.dir("${buildDir}/allure-results").optional()
    inputs.dir("${project.rootDir}/allure-results").optional()
}

// Configure allureReport task to use pulled results
afterEvaluate {
    def buildResults = file("${buildDir}/allure-results")
    
    tasks.named('allureReport') {
        // Ensure Allure commandline is downloaded first
        // The allure-download plugin adds downloadAllure task
        try {
            dependsOn tasks.named('downloadAllure')
        } catch (Exception ignored) {
            // Try alternative task name
            try {
                dependsOn tasks.named('allureDownload')
            } catch (Exception ignored2) {
                // Task might not exist, that's okay - user will need to run download manually
            }
        }
        
        // Copy results before generating report
        dependsOn tasks.named('copyAllureResults')
        
        // Ensure results directory exists and has files
        doFirst {
            if (!buildResults.exists() || !buildResults.listFiles()) {
                throw new GradleException("No Allure results found in ${buildResults.absolutePath}. Make sure to run tests and pull results first.")
            }
            println "üìä Generating Allure report from ${buildResults.listFiles().length} result files in ${buildResults.absolutePath}"
            // The Allure plugin should automatically find results in build/allure-results
        }
    }
    
    // Make allureServe depend on allureReport so it generates the report first
    tasks.named('allureServe') {
        dependsOn tasks.named('allureReport')
    }
}

private setSigningConfigKey(config, Properties props) {
    if(props != null) {
        config.storeFile = props['keystore'] == null ? null : file(props['keystore'])
        config.storePassword = props['store.pass']
        config.keyAlias = props['key.alias']
        config.keyPassword = props['key.pass']
    }
    return config
}

private static Properties loadProperties(File file) {
    Properties props = null
    if (file.canRead()) {
        props = new Properties()
        props.load(new FileInputStream(file))
    } else {
        System.err.println "\"${file}\" not found"
    }
    return props
}