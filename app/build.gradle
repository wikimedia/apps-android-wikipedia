apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

import com.android.build.gradle.internal.dsl.ProductFlavor

// Copy the signing.properties.sample file to ~/.sign/signing.properties and adjust the values.
final File PROD_PROPS_FILE = new File(System.getProperty('user.home'), '.sign/signing.properties')
final File REPO_PROPS_FILE = new File('repo.properties')
final Properties PROD_PROPS = loadProperties(PROD_PROPS_FILE)
final Properties REPO_PROPS = loadProperties(REPO_PROPS_FILE)


def static getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy-MM-dd')
    return formattedDate
}

def computeVersionName(label) {
    return "2.7.${android.defaultConfig.versionCode}-${label}-${date}"
}

final JavaVersion JAVA_VERSION = JavaVersion.VERSION_1_8
android {
    compileSdkVersion 29

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility = JAVA_VERSION
        targetCompatibility = JAVA_VERSION
    }

    dexOptions {
        preDexLibraries = true
        jumboMode = true
    }

    defaultConfig {
        applicationId 'org.wikipedia'
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 50322
        testApplicationId 'org.wikipedia.test'
        testInstrumentationRunner "org.wikipedia.WikipediaTestRunner"
        vectorDrawables.useSupportLibrary = true

        dimension 'default'
        signingConfig signingConfigs.debug

        buildConfigField "String", "DEFAULT_RESTBASE_URI_FORMAT", '"%1$s://%2$s/api/rest_v1/"'
    }

    sourceSets {
        test {
            java.srcDirs += 'src/testlib/java'
        }
        androidTest {
            java.srcDirs += 'src/testlib/java'
        }
    }

    signingConfigs {
        prod {
            setSigningConfigKey(prod, PROD_PROPS)
        }
        debug {
            setSigningConfigKey(debug, REPO_PROPS)
        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile 'test-proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile 'test-proguard-rules.pro'
        }
    }

    flavorDimensions 'default'

    productFlavors {
        dev {
            versionName computeVersionName('dev')
            applicationIdSuffix '.dev'
        }
        labsPCS {
            versionName computeVersionName('dev')
            applicationIdSuffix '.dev'
            buildConfigField "String", "DEFAULT_RESTBASE_URI_FORMAT", '"https://mobileapps.wmflabs.org/%2$s/v1/"'
        }
        localPCS {
            versionName computeVersionName('dev')
            applicationIdSuffix '.dev'
            buildConfigField "String", "DEFAULT_RESTBASE_URI_FORMAT", '"http://10.0.2.2:8888/%2$s/v1/"'
        }
        prod {
            versionName computeVersionName('r')
            signingConfig signingConfigs.prod
        }
        alpha {
            versionName computeVersionName('alpha')
            applicationIdSuffix '.alpha'
        }
        beta {
            versionName computeVersionName('beta')
            applicationIdSuffix '.beta'
            signingConfig signingConfigs.prod
        }
        amazon {
            versionName computeVersionName('amazon')
            signingConfig signingConfigs.prod
        }
        fdroid {
            versionName computeVersionName('fdroid')
            signingConfig signingConfigs.prod
        }
        custom {
            versionName computeVersionName(customChannel)
            // next line is for injecting a custom channel value into the custom/AndroidManifest.xml
            manifestPlaceholders = [customChannel:getProperty('customChannel').toString()]
            signingConfig signingConfigs.prod
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    bundle {
        language {
            enableSplit false
        }
    }
}

apply from: '../gradle/src/test.gradle'
apply from: '../gradle/src/checkstyle.gradle'

dependencies {

    // To keep the Maven Central dependencies up-to-date
    // use http://gradleplease.appspot.com/ or http://search.maven.org/.
    // Debug with ./gradlew -q app:dependencies --configuration compile

    String okHttpVersion = '4.3.1'
    String retrofitVersion = '2.6.0'
    String glideVersion = '4.11.0'
    String mockitoCore = 'org.mockito:mockito-core:1.9.5'
    String leakCanaryVersion = '2.2'
    String kotlinCoroutinesVersion = '1.2.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"

    implementation "com.google.android.material:material:1.1.0"
    implementation "androidx.core:core:1.2.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.fragment:fragment:1.2.4"
    implementation "androidx.palette:palette:1.0.0"
    implementation "androidx.preference:preference:1.1.1"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation 'com.google.android:flexbox:2.0.1'
    implementation 'com.android.installreferrer:installreferrer:1.1.2'
    implementation 'androidx.drawerlayout:drawerlayout:1.1.0-beta01'

    implementation ('com.github.michael-rapp:chrome-like-tab-switcher:0.4.6') {
        exclude group: 'org.jetbrains'
    }

    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"

    implementation 'com.google.code.gson:gson:2.8.5'
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "io.reactivex.rxjava2:rxjava:2.2.14"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.12.0'
    implementation 'com.microsoft.appcenter:appcenter-crashes:3.1.0'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'org.jsoup:jsoup:1.12.1'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    // For release builds, this provides a nearly no-op AppWatcher.
    implementation "com.squareup.leakcanary:leakcanary-object-watcher-android:$leakCanaryVersion"

    testImplementation 'junit:junit:4.12'
    testImplementation mockitoCore
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okHttpVersion"
    testImplementation "commons-io:commons-io:2.6"

    androidTestImplementation mockitoCore
    androidTestRuntimeOnly 'com.crittercism.dexmaker:dexmaker:1.4'
    androidTestImplementation 'com.crittercism.dexmaker:dexmaker-mockito:1.4'
    androidTestRuntimeOnly 'com.crittercism.dexmaker:dexmaker-dx:1.4'

    androidTestImplementation "androidx.test:rules:1.2.0" // JUnit Rules
    androidTestImplementation "androidx.test:runner:1.2.0" // Android JUnit Runner
}

private setSigningConfigKey(config, Properties props) {
    if (System.getenv("TRAVIS_PULL_REQUEST") == "false") {
        System.out.println(">>> signing using secure key.")
        config.storeFile = file("../wikipedia.keystore")
        config.storePassword = System.getenv("keystore_password")
        config.keyAlias = System.getenv("keystore_alias")
        config.keyPassword = System.getenv("keystore_alias_password")
    } else if(props != null) {
        System.out.println(">>> signing using default key.")
        config.storeFile = props['keystore'] == null ? null : file(props['keystore'])
        config.storePassword = props['store.pass']
        config.keyAlias = props['key.alias']
        config.keyPassword = props['key.pass']
    }
    return config
}

private static Properties loadProperties(File file) {
    Properties props = null
    if (file.canRead()) {
        props = new Properties()
        props.load(new FileInputStream(file))
    } else {
        System.err.println "\"${file}\" not found"
    }
    return props
}

addSyncContentProviderAuthority 'readinglists', 'reading_lists'

private void addSyncContentProviderAuthority(String path, String name) {
    android.productFlavors.all { ProductFlavor flavor ->
        String authority = "${android.defaultConfig.applicationId + flavor.applicationIdSuffix}.sync.${path}"
        resValue 'string', "${name.toLowerCase()}_authority", authority
        buildConfigField 'String', "${name.toUpperCase()}_AUTHORITY", "\"$authority\""
    }
}
