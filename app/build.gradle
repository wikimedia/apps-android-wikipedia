apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'

// Copy the signing.properties.sample file to ~/.sign/signing.properties and adjust the values.
final File PROD_PROPS_FILE = new File(System.getProperty('user.home'), '.sign/signing.properties')
final File REPO_PROPS_FILE = new File('repo.properties')
final Properties PROD_PROPS = loadProperties(PROD_PROPS_FILE)
final Properties REPO_PROPS = loadProperties(REPO_PROPS_FILE)


def static getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy-MM-dd')
    return formattedDate
}

def computeVersionName(label) {
    return "2.7.${android.defaultConfig.versionCode}-${label}-${date}"
}

final JavaVersion JAVA_VERSION = JavaVersion.VERSION_1_8

android {
    compileSdkVersion 30

    compileOptions {
        sourceCompatibility = JAVA_VERSION
        targetCompatibility = JAVA_VERSION
    }

    kotlinOptions {
        jvmTarget = JAVA_VERSION
    }

    dexOptions {
        preDexLibraries = true
        jumboMode = true
    }

    defaultConfig {
        applicationId 'org.wikipedia'
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 50350
        testApplicationId 'org.wikipedia.test'
        testInstrumentationRunner "org.wikipedia.WikipediaTestRunner"
        vectorDrawables.useSupportLibrary = true

        dimension 'default'
        signingConfig signingConfigs.debug

        buildConfigField "String", "DEFAULT_RESTBASE_URI_FORMAT", '"%1$s://%2$s/api/rest_v1/"'
        buildConfigField "String", "META_WIKI_BASE_URI", '"https://meta.wikimedia.org"'
        buildConfigField "String", "EVENTGATE_ANALYTICS_EXTERNAL_BASE_URI", '"https://intake-analytics.wikimedia.org"'
        buildConfigField "String", "EVENTGATE_LOGGING_EXTERNAL_BASE_URI", '"https://intake-logging.wikimedia.org"'
    }

    buildFeatures {
        viewBinding true
    }

    sourceSets {

        prod { java.srcDirs += 'src/extra/java' }
        beta { java.srcDirs += 'src/extra/java' }
        alpha { java.srcDirs += 'src/extra/java' }
        dev { java.srcDirs += 'src/extra/java' }
        custom { java.srcDirs += 'src/extra/java' }

        test {
            java.srcDirs += 'src/testlib/java'
        }
        androidTest {
            java.srcDirs += 'src/testlib/java'
        }
    }

    signingConfigs {
        prod {
            setSigningConfigKey(prod, PROD_PROPS)
        }
        debug {
            setSigningConfigKey(debug, REPO_PROPS)
        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile 'test-proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile 'test-proguard-rules.pro'
        }
    }

    flavorDimensions 'default'

    productFlavors {
        dev {
            versionName computeVersionName('dev')
            applicationIdSuffix '.dev'
            buildConfigField "String", "META_WIKI_BASE_URI", '"https://meta.wikimedia.beta.wmflabs.org"'
            buildConfigField "String", "EVENTGATE_ANALYTICS_EXTERNAL_BASE_URI", '"https://intake-analytics.wikimedia.beta.wmflabs.org"'
            buildConfigField "String", "EVENTGATE_LOGGING_EXTERNAL_BASE_URI", '"https://intake-logging.wikimedia.beta.wmflabs.org"'
        }
        prod {
            versionName computeVersionName('r')
            signingConfig signingConfigs.prod
        }
        alpha {
            versionName computeVersionName('alpha')
            applicationIdSuffix '.alpha'
        }
        beta {
            versionName computeVersionName('beta')
            applicationIdSuffix '.beta'
            signingConfig signingConfigs.prod
        }
        fdroid {
            versionName computeVersionName('fdroid')
            signingConfig signingConfigs.prod
        }
        custom {
            versionName computeVersionName(customChannel)
            // next line is for injecting a custom channel value into the custom/AndroidManifest.xml
            manifestPlaceholders = [customChannel:getProperty('customChannel').toString()]
            signingConfig signingConfigs.prod
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor' // required by Butter Knife
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    bundle {
        language {
            enableSplit false
        }
    }
}

apply from: '../gradle/src/test.gradle'
apply from: '../gradle/src/checkstyle.gradle'
apply from: '../gradle/src/ktlint.gradle'

dependencies {

    // To keep the Maven Central dependencies up-to-date
    // use http://gradleplease.appspot.com/ or http://search.maven.org/.
    // Debug with ./gradlew -q app:dependencies --configuration compile

    String okHttpVersion = '4.8.1'
    String retrofitVersion = '2.9.0'
    String glideVersion = '4.11.0'
    String butterknifeVersion = '10.2.0'
    String mockitoVersion = '3.4.0'
    String leakCanaryVersion = '2.5'
    String kotlinCoroutinesVersion = '1.3.9'
    String firebaseMessagingVersion = '21.0.1'
    String mlKitVersion = '16.1.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"

    implementation "com.google.android.material:material:1.3.0"
    implementation "androidx.core:core-ktx:1.3.2"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.fragment:fragment-ktx:1.3.2"
    implementation "androidx.palette:palette-ktx:1.0.0"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.recyclerview:recyclerview:1.2.0"
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation 'com.google.android:flexbox:2.0.1'
    implementation 'com.android.installreferrer:installreferrer:2.2'
    implementation 'androidx.drawerlayout:drawerlayout:1.1.1'

    implementation ('com.github.michael-rapp:chrome-like-tab-switcher:0.4.6') {
        exclude group: 'org.jetbrains'
    }

    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glideVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$glideVersion"

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$retrofitVersion"
    implementation "io.reactivex.rxjava3:rxjava:3.0.4"
    implementation "io.reactivex.rxjava3:rxandroid:3.0.0"
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    implementation 'com.microsoft.appcenter:appcenter-crashes:3.1.0'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'org.jsoup:jsoup:1.12.1'
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation "com.github.skydoves:balloon:1.3.4"

    // For language detection during editing
    prodImplementation "com.google.mlkit:language-id:$mlKitVersion"
    betaImplementation "com.google.mlkit:language-id:$mlKitVersion"
    alphaImplementation "com.google.mlkit:language-id:$mlKitVersion"
    devImplementation "com.google.mlkit:language-id:$mlKitVersion"
    customImplementation "com.google.mlkit:language-id:$mlKitVersion"

    // For receiving push notifications for logged-in users.
    prodImplementation "com.google.firebase:firebase-messaging:$firebaseMessagingVersion"
    betaImplementation "com.google.firebase:firebase-messaging:$firebaseMessagingVersion"
    alphaImplementation "com.google.firebase:firebase-messaging:$firebaseMessagingVersion"
    devImplementation "com.google.firebase:firebase-messaging:$firebaseMessagingVersion"
    customImplementation "com.google.firebase:firebase-messaging:$firebaseMessagingVersion"

    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    implementation "com.squareup.leakcanary:plumber-android:$leakCanaryVersion"

    testImplementation 'junit:junit:4.13'
    testImplementation "org.mockito:mockito-inline:$mockitoVersion"
    testImplementation 'org.robolectric:robolectric:4.3.1'
    testImplementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    testImplementation "com.squareup.okhttp3:mockwebserver:$okHttpVersion"
    testImplementation "commons-io:commons-io:2.6"
    testImplementation 'org.hamcrest:hamcrest:2.2'

    androidTestRuntimeOnly 'com.crittercism.dexmaker:dexmaker:1.4'
    androidTestImplementation 'com.crittercism.dexmaker:dexmaker-mockito:1.4'
    androidTestRuntimeOnly 'com.crittercism.dexmaker:dexmaker-dx:1.4'

    androidTestImplementation "androidx.test:rules:1.3.0" // JUnit Rules
    androidTestImplementation "androidx.test:runner:1.3.0" // Android JUnit Runner
}

private setSigningConfigKey(config, Properties props) {
    if(props != null) {
        config.storeFile = props['keystore'] == null ? null : file(props['keystore'])
        config.storePassword = props['store.pass']
        config.keyAlias = props['key.alias']
        config.keyPassword = props['key.pass']
    }
    return config
}

private static Properties loadProperties(File file) {
    Properties props = null
    if (file.canRead()) {
        props = new Properties()
        props.load(new FileInputStream(file))
    } else {
        System.err.println "\"${file}\" not found"
    }
    return props
}

addSyncContentProviderAuthority 'readinglists', 'reading_lists'

private void addSyncContentProviderAuthority(String path, String name) {
    android.productFlavors.all { flavor ->
        String authority = "${android.defaultConfig.applicationId + flavor.applicationIdSuffix}.sync.${path}"
        resValue 'string', "${name.toLowerCase()}_authority", authority
        buildConfigField 'String', "${name.toUpperCase()}_AUTHORITY", "\"$authority\""
    }
}
